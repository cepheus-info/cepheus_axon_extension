import javax.naming.ConfigurationException

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.multiplatform' version '1.5.31'
    id 'maven-publish'
    id 'signing'
}

group = 'info.cepheus'
version = '0.0.1'
archivesBaseName = 'cepheus_axon_extension'

repositories {
    mavenCentral()
}

dependencies {
    implementation enforcedPlatform("${axonPlatformGroupId}:${axonPlatformArtifactId}:${axonPlatformVersion}")
    implementation "org.axonframework:axon-configuration"
    implementation "org.axonframework:axon-modelling"
    implementation "org.axonframework:axon-messaging"

    implementation "jakarta.transaction:jakarta.transaction-api:1.3.3"
    implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:2.0.2'
    implementation 'io.projectreactor:reactor-core:3.4.11'
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '11'
        }
        withJava()
        testRuns["test"].executionTask.configure {
            useJUnitPlatform()
        }
    }

    sourceSets {
        commonMain {

        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {

        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit5')
                implementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
                runtimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
            }
        }
    }
}

ext {
    it.'signing.secretKeyRingFile' = project.findProperty('signing.secretKeyRingFile')
    it.'signing.password' = project.findProperty('signing.password')
    it.'signing.keyId' = project.findProperty('signing.keyId')
    it.ossrhUsername = project.findProperty('ossrhUsername')
    it.ossrhPassword = project.findProperty('ossrhPassword')
    it.ossrhStagingProfileId = project.findProperty('ossrhStagingProfileId')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'info.cepheus'
            artifactId 'cepheus_axon_extension'
            version '0.0.1'
        }
    }
    repositories {
        //
        // repositories to publish to
        //
        maven {
            name 'ossrh'
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    doFirst {
        if (!ossrhUsername) {
            throw new ConfigurationException('Please set the sonatype username with project property "ossrhUsername" ')
        }
        if (!ossrhPassword) {
            throw new ConfigurationException('Please set the sonatype password with project property "ossrhPassword" ')
        }
    }
}

